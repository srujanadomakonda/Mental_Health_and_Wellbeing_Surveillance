
#include <Arduino.h>
#include <ESP8266WiFi.h>
#include <Firebase_ESP_Client.h>
#include <NTPClient.h>
#include <WiFiUdp.h>
#include <SoftwareSerial.h>
struct ardData
{
  float h;
  float t;
  float myBPM;
  float x_adc_value;
  float y_adc_value;
  float z_adc_value;
  float ppm;
  float db;
}ardData;

// Provide the token generation process info.
#include "addons/TokenHelper.h"
// Provide the RTDB payload printing info and other helper functions.
#include "addons/RTDBHelper.h"

// Insert your network credentials
#define WIFI_SSID "Your Wifi_SSID"
#define WIFI_PASSWORD "Wifi_password"

// Insert Firebase project API Key
#define API_KEY "API_Key_of_Firebase"

// Insert Authorized Email and Corresponding Password
#define USER_EMAIL "Mail"
#define USER_PASSWORD "password"

// Insert RTDB URL
#define DATABASE_URL "URL"

// Define Firebase objects
FirebaseData fbdo;
FirebaseAuth auth;
FirebaseConfig config;

//incomingreadings incomingreadings;
SoftwareSerial espSerial(D5, D6);

// Variable to save USER UID
String uid;

// Database main path (to be updated in setup with the user UID)
String databasePath;
// Database child nodes
String tempPath = "/temperature";
String humPath = "/humidity";
String pulsePath = "/pulse";
String xPath = "/X";
String yPath = "/Y";
String zPath = "/Z";
String airqualityPath = "/air";
String noisePath = "/noise";

String timePath = "/timestamp"; // add more paths for others sensors

// Parent Node (to be updated in every loop)
String parentPath;

FirebaseJson json;

// Define NTP Client to get time
WiFiUDP ntpUDP;
NTPClient timeClient(ntpUDP, "pool.ntp.org");

// Variable to save current epoch time
int timestamp;


// Timer variables (send new readings every three minutes)
unsigned long sendDataPrevMillis = 0;
unsigned long timerDelay = 60000;


// Initialize WiFi
void initWiFi() {
  WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
  Serial.print("Connecting to WiFi ..");
  while (WiFi.status() != WL_CONNECTED) {
    Serial.print('.');
    delay(1000);
  }
  Serial.println(WiFi.localIP());
  Serial.println();
}

// Function that gets current epoch time
unsigned long getTime() {
  timeClient.update();
  unsigned long now = timeClient.getEpochTime();
  return now;
}

void setup(){
  Serial.begin(115200);
  espSerial.begin(115200);
  // Initialize DHT11 sensor
  initWiFi();
  timeClient.begin();

  // Assign the api key (required)
  config.api_key = API_KEY;

  // Assign the user sign in credentials
  auth.user.email = USER_EMAIL;
  auth.user.password = USER_PASSWORD;

  // Assign the RTDB URL (required)
  config.database_url = DATABASE_URL;

  Firebase.reconnectWiFi(true);
  fbdo.setResponseSize(4096);

  // Assign the callback function for the long running token generation task */
  config.token_status_callback = tokenStatusCallback; //see addons/TokenHelper.h

  // Assign the maximum retry of token generation
  config.max_token_generation_retry = 5;

  // Initialize the library with the Firebase authen and config
  Firebase.begin(&config, &auth);

  // Getting the user UID might take a few seconds
  Serial.println("Getting User UID");
  while ((auth.token.uid) == "") {
    Serial.print('.');
    delay(1000);
  }
  // Print user UID
  uid = auth.token.uid.c_str();
  Serial.print("User UID: ");
  Serial.println(uid);

  // Update database path
  databasePath = "/UsersData/" + uid + "/readings";
}

void loop(){

  // Send new readings to database
  if (Firebase.ready() && (millis() - sendDataPrevMillis > timerDelay || sendDataPrevMillis == 0)){
    sendDataPrevMillis = millis();

    //Get current timestamp
    timestamp = getTime();
    Serial.print ("time: ");
    Serial.println (timestamp);

    parentPath= databasePath + "/" + String(timestamp);

    if (espSerial.available() >= sizeof(ardData)) 
  {
    espSerial.readBytes((char*)&ardData, sizeof(ardData));
    Serial.println("data received");
    Serial.print("TEMPERATURE: ");
    Serial.println(ardData.t);
    Serial.print("HUMIDITY: ");
    Serial.println(ardData.h);
    Serial.print("PULSE: ");
    Serial.println(ardData.myBPM);
    Serial.print("X: ");
    Serial.println(ardData.x_adc_value);
    Serial.print("Y: ");
    Serial.println(ardData.y_adc_value);
    Serial.print("Z: ");
    Serial.println(ardData.z_adc_value);
    Serial.print("AIR QUALITY: ");
    Serial.println(ardData.ppm);
    Serial.print("VOICE: ");
    Serial.println(ardData.db);
  }

    json.set(tempPath.c_str(), String(ardData.t));
    json.set(humPath.c_str(), String(ardData.h));
    json.set(pulsePath.c_str(), String(ardData.myBPM));
    json.set(xPath.c_str(), String(ardData.x_adc_value));
    json.set(yPath.c_str(), String(ardData.y_adc_value));
    json.set(zPath.c_str(), String(ardData.z_adc_value));
    json.set(airqualityPath.c_str(), String(ardData.ppm));
    json.set(noisePath.c_str(), String(ardData.db));



    json.set(timePath, String(timestamp));
    Serial.printf("Set json... %s\n", Firebase.RTDB.setJSON(&fbdo, parentPath.c_str(), &json) ? "ok" : fbdo.errorReason().c_str());
  }
}
